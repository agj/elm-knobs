
print "‚ÑπÔ∏è Checking package version consistency‚Ä¶"

let currentVersion = open elm.json | get 'version'

print $"‚ÑπÔ∏è Current version: ($currentVersion)"

def checkHasCurrentVersion [place versions] {
  print $"üîç Checking that ($place) has the current version‚Ä¶"
  if ($currentVersion not-in $versions) {
    print $"‚ùå Current version not found."
    exit 1
  }
}

def checkHasOutdatedVersion [place versions] {
  let outdatedVersions = $versions
    | filter { $in != $currentVersion }
  print $"üîç Checking if outdated versions remain in ($place)‚Ä¶"
  if ($outdatedVersions | length | $in > 0) {
    print $"‚ùå Outdated versions found in ($place)."
    print $outdatedVersions
    exit 1
  }
}

def getVersionsUsedInLinks [filesGlob] {
  open $filesGlob
    | split row "\n"
    | each { parse --regex 'elm-knobs/([a-z0-9]+/)?([0-9.]+)' }
    | filter { length | $in > 0 }
    | each { get 0 | get capture1 }
}

# Changelog

let allVersionsInChangelog = open CHANGELOG.md
  | split row "\n"
  | each { parse --regex '^## \[([0-9.]+)\]' }
  | filter { length | $in > 0 }
  | each { get 0 | get capture0 }

checkHasCurrentVersion "changelog" $allVersionsInChangelog

# Git tags

let allVersionsInGitTags = (^git tag)
  | split row "\n"
  | find --regex '^[0-9.]+$'

checkHasCurrentVersion "git tags" $allVersionsInGitTags

# Links in code

let allVersionsInCode = getVersionsUsedInLinks ./src/*.elm

checkHasOutdatedVersion "code" $allVersionsInCode

# Links in readme

let allVersionsInReadme = getVersionsUsedInLinks ./README.md

checkHasOutdatedVersion "readme" $allVersionsInReadme

# Interactive docs version

let versionInInteractiveDocs = open ./interactive-docs/src/Constants.elm
  | parse --regex '    "([0-9.]+)"'
  | get capture0
  | first

checkHasCurrentVersion "interactive docs" $versionInInteractiveDocs

# Interactive docs file

let gitHasInteractiveDocsCurrentVersionFileCheck = $"HEAD:./interactive-docs/output/($currentVersion)/index.html"
  | do { ^git cat-file -e $in }
  | complete

print $"üîç Checking if there is interactive documentation for the current version‚Ä¶"

if ($gitHasInteractiveDocsCurrentVersionFileCheck | get exit_code) != 0 {
  print "‚ùå Interactive documentation for the current version not found in git."
  exit 1
}


print "‚úÖ Current version checks OK."
